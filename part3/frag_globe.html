<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
<script type="text/javascript" src="jquery.js"></script>
</head>

<body>


<div id="msgid">
</div>

<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1024" height="768" tabindex="1"></canvas>


<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        v_positionMC = Position;
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;

    uniform float u_time;
    uniform mat4 u_InvTrans;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);

    float NIGHT_EPSILON = 0.1;
    float bumpMapWidth = 1024.0;
    float bumpMapHeight = 512.0;

    void main(void)
    {
        // surface normal - normalized after rasterization
        //vec3 normal = normalize(v_Normal);
        // normalized eye-to-position vector in camera coordinates
        vec3 eyeToPosition = normalize(v_Position);

        //compute normal from bump map
        float texelWidth = 1.0/bumpMapWidth;
        float texelHeight = 1.0/bumpMapHeight;
        float centerTexel = texture2D(u_Bump, v_Texcoord).r;
        float rightTexel = texture2D(u_Bump, vec2(v_Texcoord.x + texelWidth, v_Texcoord.y)).r;
        float topTexel = texture2D(u_Bump, vec2(v_Texcoord.x, v_Texcoord.y + texelHeight)).r;
        vec3 tgtSpaceNormal = normalize(vec3(centerTexel - rightTexel, centerTexel - topTexel, 0.2));

        //this matrix brings us from tangent space to eye space
        vec3 origNormal = normalize(v_Normal); //cloud normal
        mat3 tgtToEye = eastNorthUpToEyeCoordinates(v_positionMC, origNormal);

        //normal in eye/camera coordinates
        vec3 normal = normalize(tgtToEye * tgtSpaceNormal);
        
        float diffuse = clamp(dot(u_CameraSpaceDirLight, normal), 0.0, 1.0);
        float cloudDiffuse = clamp(dot(u_CameraSpaceDirLight, origNormal), 0.0, 1.0);

        vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, normal);
        float specular = max(dot(toReflectedLight, -eyeToPosition), 0.0);
        specular = pow(specular, 20.0);

        float gammaCorrect = 1.0/1.2; //gamma correct by 1/1.2

        vec2 offsetTexcoord = v_Texcoord;
        offsetTexcoord.s += 0.1*u_time; 

        vec3 dayColor = texture2D(u_DayDiffuse, v_Texcoord).rgb;
        vec3 cloudColor = texture2D(u_Cloud, offsetTexcoord).rgb; 
        vec3 cloudOpacity = texture2D(u_CloudTrans, offsetTexcoord).rgb; 
        vec3 surfNightColor = texture2D(u_Night, v_Texcoord).rgb;
        vec3 specCoeff = texture2D(u_EarthSpec, v_Texcoord).rgb;
        //apply gamma correction to nighttime texture
        surfNightColor = pow(surfNightColor,vec3(gammaCorrect));
 
        //surface color under clouds
        vec3 surfColor = ((0.6 * diffuse) + (0.4 * specCoeff * specular)) * dayColor;
        vec3 color = mix( cloudDiffuse * cloudColor, surfColor, cloudOpacity.r); //assuming cloud transparency is black/white
        vec3 black = vec3(0.0, 0.0, 0.0);
        vec3 nightColor = mix(black, surfNightColor, cloudOpacity.r);

        vec3 blended_color = mix(nightColor, color, (1.0/NIGHT_EPSILON)*diffuse);
        vec3 final_color;
        if(diffuse < NIGHT_EPSILON){
            final_color = blended_color;
        }
        else{
            final_color = color;
        }
        
        float rim_factor = dot(v_Normal, v_Position) + 1.0;
        if( rim_factor > 0.0 ){
            final_color = final_color + vec3(rim_factor/4.0, rim_factor/2.0, rim_factor/2.0);
        }
        gl_FragColor = vec4(final_color, 1.0);
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }
</script>

<script id="iss-fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;

    uniform float u_time;
    uniform mat4 u_InvTrans;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);

    float NIGHT_EPSILON = 0.1;
    float bumpMapWidth = 1024.0;
    float bumpMapHeight = 512.0;

    void main(void)
    {
        vec3 dayColor = texture2D(u_DayDiffuse, v_Texcoord).rgb;
        gl_FragColor = vec4(dayColor, 1.0);
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }
</script>

<script src ="gl-matrix.js" type ="text/javascript"></script>
<script src ="webGLUtility.js" type ="text/javascript"></script>
<script src ="iss.js" type ="text/javascript"></script>
<script src ="frag_globe.js" type ="text/javascript"></script>
</body>

</html>
