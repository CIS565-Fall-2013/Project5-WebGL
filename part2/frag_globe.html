<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
</head>

<body>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1024" height="768" tabindex="1"></canvas>

<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        v_positionMC = Position;
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;

    uniform float u_time;
    uniform float u_water;
    uniform mat4 u_InvTrans;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);
    float rand(vec2 seed);

    vec3 permute(vec3 x) {
        x = ((x*34.0)+1.0)*x;
        return x - floor(x * (1.0 / 289.0)) * 289.0;
    }

    float simplexNoise(vec2 v)
    {
        const vec4 C = vec4(0.211324865405187, 0.366025403784439,  -0.577350269189626, 0.024390243902439); 

        vec2 i  = floor(v + dot(v, C.yy) );
        vec2 x0 = v -   i + dot(i, C.xx);

        vec2 i1;
        i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);

        vec4 x12 = x0.xyxy + C.xxzz;
        x12.xy -= i1;

        i = i - floor(i * (1.0 / 289.0)) * 289.0;

        vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
            + i.x + vec3(0.0, i1.x, 1.0 ));

        vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
        m = m*m ;
        m = m*m ;

        vec3 x = 2.0 * fract(p * C.www) - 1.0;
        vec3 h = abs(x) - 0.5;
        vec3 ox = floor(x + 0.5);
        vec3 a0 = x - ox;

        m *= inversesqrt( a0*a0 + h*h );

        vec3 g;
        g.x  = a0.x  * x0.x  + h.x  * x0.y;
        g.yz = a0.yz * x12.xz + h.yz * x12.yw;
        return 130.0 * dot(m, g);
    }

    void main(void)
    {
        // surface normal - normalized after rasterization
        vec3 normal = normalize(v_Normal);
        // normalized eye-to-position vector in camera coordinates
        vec3 eyeToPosition = normalize(v_Position);

        float center = texture2D(u_Bump, v_Texcoord).r;
        float top = texture2D(u_Bump, v_Texcoord+vec2(0.0,1.0/512.0)).r;
        float right = texture2D(u_Bump, v_Texcoord+vec2(1.0/1024.0,0.0)).r;

        vec3 normalBump = normalize(vec3(center - right, center - top, 0.2));
        
        if (texture2D(u_EarthSpec, v_Texcoord).r > 0.3){
            normalBump.x = .0125 * simplexNoise(45.0*vec2(v_positionMC)-22.0*vec2(u_time*0.1, u_time*0.1)) * 
                                 simplexNoise(20.0*vec2(v_positionMC.z-15.0*u_time*0.1,v_positionMC.y-7.0*u_time*0.1));
        }

        vec3 normalBumpEye = eastNorthUpToEyeCoordinates(v_positionMC, normal) * normalBump;

        // float a =2.0;

        // float s_contrib = a*sin(v_Texcoord.s*200.0*3.14159 + u_water);
        // float t_contrib = a*cos(v_Texcoord.t*200.0*3.14159 + u_water);
        // float centerWater = s_contrib*t_contrib;
        // s_contrib = a*sin(v_Texcoord.s*200.0*3.14159 + u_water);
        // t_contrib = a*cos(v_Texcoord.t*200.0*3.14159 + u_water)+1.0/512.0;
        // float topWater = s_contrib*t_contrib;
        // s_contrib = a*sin(v_Texcoord.s*200.0*3.14159 + u_water+1.0/1024.0);
        // t_contrib = a*cos(v_Texcoord.t*200.0*3.14159 + u_water+1.0/512.0);
        // float rightWater = s_contrib*t_contrib;
        // vec3 normalWaterBump = normalize(vec3(centerWater - rightWater, centerWater - topWater, 0.2));
        // vec3 normalWaterBumpEye = eastNorthUpToEyeCoordinates(v_positionMC, normal) * normalWaterBump;
        //vec3 waterColor = vec3(height+.5, height+.5, height+.5)*texture2D(u_EarthSpec, v_Texcoord).r;

        float diffuse = clamp(dot(u_CameraSpaceDirLight, normalBumpEye), 0.0, 1.0);

        vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, normalBumpEye);
        float specular = max(dot(toReflectedLight, -eyeToPosition), 0.0);
        specular = pow(specular, 10.0);
        specular *= texture2D(u_EarthSpec, v_Texcoord).r;

        float gammaCorrect = 1.0/1.2; //gamma correct by 1/1.2

        vec3 dayColor = texture2D(u_DayDiffuse, v_Texcoord).rgb;
        vec3 nightColor = texture2D(u_Night, v_Texcoord).rgb;
        vec3 cloudColor = texture2D(u_Cloud, v_Texcoord-vec2(u_time*0.05,0.0)).rgb;
        float cloudAlpha = texture2D(u_CloudTrans, v_Texcoord-vec2(u_time*0.05,0.0)).r;

       // dayColor += normalBumpWater*specular;
        
        specular *= cloudAlpha;

        //apply gamma correction to nighttime texture
        nightColor = pow(nightColor,vec3(gammaCorrect));

        nightColor = mix( vec3(.2,.2,.3), nightColor, cloudAlpha);
        dayColor = mix(cloudColor, dayColor, cloudAlpha);

        vec3 color = mix(nightColor, ((0.6 * diffuse) + (0.4 * specular)) * dayColor, (diffuse+1.0)/2.0);

        float rim = dot(v_Normal, v_Position)+1.0;

        if (rim > 0.0){
            color += vec3(rim/4.0, rim/2.0, rim/2.0);
        }


        gl_FragColor = vec4(color, 1.0);
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }

    float rand(vec2 co){
        return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
    }
</script>

<script src ="gl-matrix.js" type ="text/javascript"></script>
<script src ="webGLUtility.js" type ="text/javascript"></script>
<script src ="frag_globe.js" type ="text/javascript"></script>
</body>

</html>
