<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
</head>

<body>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1024" height="768" tabindex="1"></canvas>

<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        v_positionMC = Position;
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;

    uniform float u_time;
    uniform mat4 u_InvTrans;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    float genwave( vec2 pos, float time );
    float simplexNoise(vec2 v);
    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);

    void main(void)
    {
        // surface normal - normalized after rasterization
        vec3 normal = normalize(v_Normal);
        // normalized eye-to-position vector in camera coordinates
        vec3 eyeToPosition = normalize(v_Position);
       
        float diff = dot(u_CameraSpaceDirLight, normal);

        vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, normal);
        float specular = max(dot(toReflectedLight, -eyeToPosition), 0.0);
        specular = pow(specular, 40.0);

        // isOcean mask 
        float isOcean = texture2D( u_EarthSpec, v_Texcoord ).r;
        // Remove specular if we are on land because its not shiny
        vec3 bnormal;
        if ( isOcean < 1.0 ) {
          specular = 0.0; 

          // Bump mapping
          float center = texture2D( u_Bump, v_Texcoord ).r; 
          float right = texture2D( u_Bump, vec2(v_Texcoord.x+1.0/1024.0, v_Texcoord.y)).r;
          float top = texture2D( u_Bump, vec2(v_Texcoord.x, v_Texcoord.y+1.0/512.0) ).r; 
          bnormal = normalize(vec3(center - right, center - top, 0.1));
          bnormal = normalize(eastNorthUpToEyeCoordinates( v_positionMC, -eyeToPosition)*bnormal);
        } else {
  
          // Ocean waves and such
          float xfreq =200.0;
          float yfreq = 400.0;
          float wave_scl = 0.4;
          float scl = 0.5; 

          float pnoise = 100.0*simplexNoise( vec2( 0.05*u_time, v_Texcoord.y*v_Texcoord.x) );
          
          float cnoise = scl*simplexNoise( vec2(u_time + v_Texcoord.x*xfreq, v_Texcoord.y*yfreq) ); 
          float rnoise = scl*simplexNoise( vec2(u_time + (v_Texcoord.x+1.0/1024.0)*xfreq, v_Texcoord.y*yfreq) );
          float tnoise = scl*simplexNoise( vec2(u_time + v_Texcoord.x*xfreq, (v_Texcoord.y+1.0/512.0)*yfreq) );
          // Primary waves
          /*
          float center = sin( min(xfreq*v_Texcoord.x + 50.0*u_time + cnoise );
          float right =  sin( xfreq*(v_Texcoord.x + 1.0/1024.0) + 50.0*u_time + rnoise);
          float top  =  sin( xfreq*v_Texcoord.x + 50.0*u_time + tnoise);
          */
          float tscl = 10.0;
          float center = cnoise + wave_scl*sin( xfreq*v_Texcoord.y + tscl*(u_time+pnoise) + cnoise );
          float right =  rnoise + wave_scl*sin( xfreq*(v_Texcoord.y + 1.0/1024.0) + tscl*(u_time+pnoise) + rnoise);
          float top  =  tnoise + wave_scl*sin( xfreq*v_Texcoord.y + tscl*(u_time+pnoise) + tnoise);

          // Secondary waves
          

          bnormal = normalize(vec3(center - right, center - top, 2.0));
          bnormal = normalize(eastNorthUpToEyeCoordinates( v_positionMC, -eyeToPosition)*bnormal);
        } 
      
        float cloudDiffuse = clamp( diff, 0.0, 1.0 );
        float diffuse = clamp( cloudDiffuse - dot(u_CameraSpaceDirLight, bnormal), 0.0, 1.0);

        vec3 dayColor = texture2D( u_DayDiffuse, v_Texcoord ).rgb;
        vec3 nightColor = texture2D( u_Night, v_Texcoord ).rgb;

        // Read in cloud data
        vec2 cloudCoord = vec2(v_Texcoord.x + 25.0*u_time/1024.0, v_Texcoord.y);
        vec3 cloudColor = texture2D( u_Cloud, cloudCoord ).rgb;
        vec3 cloudTrans = texture2D( u_CloudTrans, cloudCoord ).rgb;
        
        float gammaCorrect = 1.0/1.2; //gamma correct by 1/1.2
        //apply gamma correction to nighttime texture
        nightColor = pow(nightColor,vec3(gammaCorrect));

        // Mix blending factor  
        float range = 0.6;
        float alpha = clamp(0.5*(diff + 1.0)/range, 0.0, 1.0);
        vec3 color = mix( nightColor, ((0.6 * diffuse) + (0.4 * specular)) * dayColor, alpha );
        // Mix in cloud colors
        color = mix( cloudDiffuse*cloudColor, color, cloudTrans );

        // Apply Rim Lighting to create an atmosphere effect
        float rim_factor = dot( v_Normal, v_Position ) + 1.0;
        // If rim_factor is greater than zero then add some atmospheric blue
        if ( rim_factor > 0.0 ) 
          color += vec3( rim_factor/4.0, rim_factor/2.0, rim_factor/1.0 );

        gl_FragColor = vec4(color, 1.0);
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }

    vec3 permute(vec3 x) {
      x = ((x*34.0)+1.0)*x;
      return x - floor(x * (1.0 / 289.0)) * 289.0;
    }
    // Simple Simplex noise implementation
    float simplexNoise(vec2 v)
      {
      const vec4 C = vec4(0.211324865405187, 0.366025403784439,  -0.577350269189626, 0.024390243902439);

      vec2 i  = floor(v + dot(v, C.yy) );
      vec2 x0 = v -   i + dot(i, C.xx);

      vec2 i1;
      i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);

      vec4 x12 = x0.xyxy + C.xxzz;
      x12.xy -= i1;

      i = i - floor(i * (1.0 / 289.0)) * 289.0;

      vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
            + i.x + vec3(0.0, i1.x, 1.0 ));

      vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
      m = m*m ;
      m = m*m ;

      vec3 x = 2.0 * fract(p * C.www) - 1.0;
      vec3 h = abs(x) - 0.5;
      vec3 ox = floor(x + 0.5);
      vec3 a0 = x - ox;

      m *= inversesqrt( a0*a0 + h*h );

      vec3 g;
      g.x  = a0.x  * x0.x  + h.x  * x0.y;
      g.yz = a0.yz * x12.xz + h.yz * x12.yw;
      return 130.0 * dot(m, g);
    }
</script>

<script src ="gl-matrix.js" type ="text/javascript"></script>
<script src ="webGLUtility.js" type ="text/javascript"></script>
<script src ="frag_globe.js" type ="text/javascript"></script>
</body>

</html>
