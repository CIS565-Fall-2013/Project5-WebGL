<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
</head>

<body>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1024" height="768" tabindex="1"></canvas>

<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        v_positionMC = Position;
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    #extension GL_OES_standard_derivatives:enable
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;

    uniform float u_time;
    uniform mat4 u_InvTrans;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);
    mat3 eyeToTangentSpace( vec3 posEC, vec3 normal );
    void main(void)
    {
        // surface normal - normalized after rasterization
        vec3 normal = normalize(v_Normal);
        // normalized eye-to-position vector in camera coordinates
        vec3 eyeToPosition = normalize(v_Position);

        //retrieve bump map value and approximate the normal
        float height = texture2D( u_Bump, v_Texcoord ).r;
        float heightRight = texture2D( u_Bump, vec2( v_Texcoord.x + 0.0009765625, v_Texcoord.y ) ).r; 
        float heightUp = texture2D( u_Bump, vec2( v_Texcoord.x, v_Texcoord.y + 0.001953125 ) ).r;

        //Rim factor 
        float rim_factor = dot( normal, eyeToPosition ) + 1.0;
        
        //calcuate normal of land 
        vec3 landNormal = normalize( vec3( height - heightRight, height - heightUp, 0.2 ) );
        mat3 eyeToTangent = eastNorthUpToEyeCoordinates( v_positionMC, normal );
        landNormal = normalize( eyeToTangent*landNormal );
        
        float diffuse = clamp(dot(u_CameraSpaceDirLight, normal ), 0.0, 1.0);
        float landDiffuse = clamp(dot(u_CameraSpaceDirLight, landNormal ), 0.0, 1.0);
        float nightDiffuse= -clamp(dot(u_CameraSpaceDirLight, normal)*5.0, -1.0, 0.0 );

        vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, normal);
        float specular = max(dot(toReflectedLight, -eyeToPosition), 0.0);
        specular = pow(specular, 20.0);

        float gammaCorrect = 1.0/1.2; //gamma correct by 1/1.2


        vec3 dayColor = texture2D(u_DayDiffuse, v_Texcoord).rgb;
        vec3 nightColor = texture2D(u_Night, v_Texcoord).rgb;
        vec2 cloudTexcoord = vec2( v_Texcoord.x+u_time, v_Texcoord.y );

        vec3 specularMask = texture2D( u_EarthSpec, v_Texcoord ).rgb;
        vec3 cloudColor =  texture2D( u_Cloud, cloudTexcoord ).rgb;
        float cloudTrans = texture2D( u_CloudTrans, cloudTexcoord ).r;

        //apply gamma correction to nighttime texture
        nightColor = pow(nightColor,vec3(gammaCorrect));
        
        vec3 color = ((0.6 * landDiffuse) + (0.4 * specular * specularMask )) * dayColor;
        
        //mat3 eyeToTangent = eyeToTangentSpace( v_Position, normal );
        eyeToTangent = eastNorthUpToEyeCoordinates( v_positionMC, normal );
        
        //calculate light vector in tangent space
        vec3 light = normalize( eyeToTangent * (u_CameraSpaceDirLight - v_Position));
        
        vec2 texcoord2 = cloudTexcoord + 0.04 * light.xy;
        float cloudshadow = texture2D( u_CloudTrans, texcoord2 ).r;

        color *= cloudshadow;

        color = mix( cloudColor*diffuse, color, cloudTrans );

        color += 2.0 * mix( vec3(0,0,0), mix( vec3(0,0,0), nightColor,cloudTrans), nightDiffuse );
        
        
        vec4 rimColor = vec4( rim_factor/4.0, rim_factor/2.0, rim_factor/2.0 ,1.0) * smoothstep( 0.7,  1.0, rim_factor );
        gl_FragColor = vec4(color, 1.0) + rimColor;

    
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }

    //This function returns a matrix transforming vectors from eye space to tangent space
    // mat3 eyeToTangentSpace( vec3 posEC, vec3 normal )
    // {

    //     vec3 T = dFdx( posEC );
    //     vec3 B = dFdy( posEC );
       
    //     T = normalize( T );
    //     B = normalize( B );
  
    //     return mat3(  T, B, normal  );
    // }
</script>

<script src ="gl-matrix.js" type ="text/javascript"></script>
<script src ="webGLUtility.js" type ="text/javascript"></script>
<script src ="frag_globe.js" type ="text/javascript"></script>
</body>

</html>
