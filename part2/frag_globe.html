<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
</head>

<body>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1024" height="768" tabindex="1"></canvas>

<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        v_positionMC = Position;
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision highp float;
    #define M_PI 3.1415926535897932384626433832795
       
    uniform sampler2D u_DayDiffuse; //Diffuse texture map for the day   
    uniform sampler2D u_Night;      //Ambient texture map for the night   
    uniform sampler2D u_Cloud;      //Color map for the clouds  
    uniform sampler2D u_CloudTrans; //Transparency map for the clouds.  Note that light areas are where clouds are NOT; Dark areas are where clouds are present
    uniform sampler2D u_EarthSpec;  //Mask of which areas of the earth have specularity. Oceans are specular, landmasses are not  
    uniform sampler2D u_Bump;       //Bump map

    uniform vec3 u_CameraSpaceDirLight; //normalized view-Space light direction  
    uniform float u_time;
    uniform mat4 u_InvTrans;
    uniform mat4 u_View;
    uniform mat4 u_ModelViewInverse;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);
    vec3 cloudIntersectionTest(vec3 sphereCenter, float cloudHeight, vec3 surfacePosition, vec3 direction);

    void main(void)
    {
        vec3 height        = texture2D(u_Bump, vec2(v_Texcoord.s,           v_Texcoord.t)).rgb;
        vec3 heightUp      = texture2D(u_Bump, vec2(v_Texcoord.s,           v_Texcoord.t+1.0/1024.0)).rgb;
        vec3 heightRight   = texture2D(u_Bump, vec2(v_Texcoord.s+1.0/512.0, v_Texcoord.t)).rgb;
        vec3 tangentNormal = normalize(vec3(height.y - heightRight.y, height.y - heightUp.y, 0.2));

        // surface normal - normalized after rasterization
        vec3 normal = normalize(v_Normal);
        vec3 normalNoBump = normal;  

        // perturbed normal
        mat3 tangnetToEye = eastNorthUpToEyeCoordinates(v_positionMC, normal);
        //mat3 test = inverse(tangnetToEye);
        normal = normalize(tangnetToEye * tangentNormal);
       
        // normalized eye-to-position vector in camera coordinates
        vec3 eyeToPosition = normalize(v_Position);
    
        float diffuse = clamp(dot(u_CameraSpaceDirLight, normal), 0.0, 1.0);
        float diffuseNoBump = clamp(dot(u_CameraSpaceDirLight, normalNoBump), 0.0, 1.0);

        vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, normal);
        float specular = max(dot(toReflectedLight, -eyeToPosition), 0.0);
        specular = pow(specular, 20.0);
        specular *= texture2D(u_EarthSpec, v_Texcoord).r;

        // get colors from maps
        vec3 cloudColor = texture2D(u_Cloud, vec2(v_Texcoord.s - 0.1*u_time, v_Texcoord.t)).rgb; // range of v_Texcoord.s and v_Texcoord.t [0, size], TEXTURE_WRAP enabled
        vec3 cloudTrans = texture2D(u_CloudTrans, vec2(v_Texcoord.s - 0.1*u_time, v_Texcoord.t)).rgb;
        //vec3 cloudColor = texture2D(u_Cloud, vec2(v_Texcoord.s, v_Texcoord.t)).rgb; // range of v_Texcoord.s and v_Texcoord.t [0, size], TEXTURE_WRAP enabled
        //vec3 cloudTrans = texture2D(u_CloudTrans, vec2(v_Texcoord.s, v_Texcoord.t)).rgb;
        vec3 dayColor   = texture2D(u_DayDiffuse, v_Texcoord).rgb;        
        vec3 nightColor = texture2D(u_Night, v_Texcoord).rgb;

        //apply gamma correction to nighttime texture
        float gammaCorrect = 1.0/1.2; //gamma correct by 1/1.2
        nightColor = pow(nightColor,vec3(gammaCorrect));

        vec3 color = (0.6 * diffuse + 0.4 * specular) * dayColor;
        cloudColor = diffuseNoBump * cloudColor;

        float cloudHeight = 0.02;  // the height of cloud is about 2% of earth's radius
        vec3 centerOfSphere = (u_View * vec4(vec3(0.0), 1.0)).xyz;
        vec3 cloudIntersectionVS = cloudIntersectionTest(centerOfSphere, cloudHeight, v_Position, u_CameraSpaceDirLight);  // find intersection between light vector and cloud
        vec3 cloudIntersectionMS = normalize((u_ModelViewInverse * vec4(cloudIntersectionVS, 1.0)).xyz);  // map the intersection back to earth's surface

        // find the texture coordinates of projection of intersection point
        float zenith = acos(cloudIntersectionMS.y);
        float azimuth = atan(cloudIntersectionMS.x, cloudIntersectionMS.z);
        if(azimuth<0.0) azimuth += 2.0*M_PI; 
        vec2 shadowTextureCoords = vec2(0.5/M_PI*azimuth - 0.1*u_time, -1.0/M_PI*zenith + 1.0); 

        // use the transparency map as color of shadow cast by clouds
        vec3 shadowColor = texture2D(u_CloudTrans, vec2(shadowTextureCoords.s, shadowTextureCoords.t)).rgb;
        color = mix(vec3(0.0), color, shadowColor.r);
        
        color = mix(cloudColor, color, cloudTrans.r);

        // TODO: get rid of the branching
        if(diffuseNoBump<0.2) color = mix(mix(vec3(0.0), nightColor, cloudTrans.r), color, diffuseNoBump/0.2); 

        float rimFactor = dot(v_Position, v_Normal) + 1.0;
        
        rimFactor = max(rimFactor, 0.0);
        vec3 rimColor = vec3(rimFactor/4.0, rimFactor/2.0, rimFactor/2.0);
        color += rimColor;

        gl_FragColor = vec4(color, 1.0);
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.y, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }
    vec3 cloudIntersectionTest(vec3 sphereCenter, float cloudHeight, vec3 surfacePosition, vec3 direction){

        float a = dot(direction, direction);
        float b = 2.0 * dot(direction, surfacePosition - sphereCenter);
        float c = dot(surfacePosition - sphereCenter, surfacePosition - sphereCenter) - (1.0+cloudHeight)*(1.0+cloudHeight);

	    float t;
        float disc = b * b - 4.0 * a * c;
        if (disc < 0.0) return vec3(0.0);

        float distSqrt = sqrt(disc);
        float q;
        if (b < 0.0) q = (-b + distSqrt)/2.0;
        else         q = (-b - distSqrt)/2.0;

        float t0 = q / a;
        float t1 = c / q;

        // make sure t0 is smaller than t1
        if (t0 > t1)
        {
            float temp = t0;
            t0 = t1;
            t1 = temp;
        }

        // if t1 is less than zero, the object is in the ray's negative direction and consequently the ray misses the sphere
        if (t1 < 0.0) return vec3(0.0);

        // if t0 is less than zero, the intersection point is at t1
        if (t0 < 0.0) t = t1;

        // else the intersection point is at t0	
        else  t = t0;

	    vec3 IntersectionPoint = surfacePosition + t*direction;
        return IntersectionPoint;
    }

</script>
<script id="skyboxVS" type="x-shader/x-vertex">
    precision highp float;

    attribute vec3 Position;
    attribute vec3 Normal;

    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    //varying vec2 v_Texcoord;
    varying vec3 v_Normal;
    varying vec3 v_Position;

    void main(void)
    {
        vec4 camera = u_View * vec4(Position, 1.0);
        v_Position = camera.xyz;
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        gl_Position = u_Persp * camera;
    }
</script>
<script id="skyboxFS" type="x-shader/x-fragment">
    precision highp float;

    uniform vec3 u_CameraSpaceLightPos; //normalized view-Space light direction  

    varying vec3 v_Normal;              // surface normal in camera coordinates
    //varying vec2 v_Texcoord;
    varying vec3 v_Position;      
    
    void main(void)
    {
        vec3 L = normalize(u_CameraSpaceLightPos - v_Position);
        vec3 normal = normalize(v_Normal);
        float diffuse = clamp(dot(L, normal), 0.0, 1.0);
        gl_FragColor = vec4(vec3(0.5), 1.0);
    }    
</script>
<script>
    var elapsedTime = 0;
    var frameCount = 0;
    var lastTime = 0;

    function drawScene() {

        // draw scene here
        var now = new Date().getTime();
        frameCount++;
        elapsedTime += (now - lastTime);
        lastTime = now;
        if (elapsedTime >= 1000) {
            fps = frameCount;
            frameCount = 0;
            elapsedTime -= 1000;
            document.getElementById('test').innerHTML = fps;
        }
    }
    lastTime = new Date().getTime();
    setInterval(drawScene, 33);

</script>
<div id="test"></div>
<script src ="gl-matrix.js" type ="text/javascript"></script>
<script src ="webGLUtility.js" type ="text/javascript"></script>
<script src ="frag_globe.js" type ="text/javascript"></script>
</body>

</html>
