<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
</head>

<body>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1600" height="900" tabindex="1"></canvas>

<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        v_positionMC = Position;
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;

    uniform float u_time;
    uniform mat4 u_InvTrans;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);
    vec3 permute(vec3 x);
    float simplexNoise(vec2 v);

    void main(void)
    {
        // Bump mapping
        vec2 v_TexcoordRight = v_Texcoord + vec2(1.0 / 1024.0, 0.0);
        vec2 v_TexcoordAbove = v_Texcoord + vec2(0.0, 1.0 / 512.0);
        float center = texture2D(u_Bump, v_Texcoord).s;
        float right = texture2D(u_Bump, v_TexcoordRight).s;
        float top = texture2D(u_Bump, v_TexcoordAbove).s;
        vec3 perturbedNormal = normalize(vec3(center - right, center - top, 0.2));
        vec3 normal = normalize(eastNorthUpToEyeCoordinates(v_positionMC, v_Normal) * perturbedNormal);

        // Use earthSpecular to find the ocean area
        vec3 earthSpecular = texture2D(u_EarthSpec, v_Texcoord).rgb;
        // If the fragment is the ocean add the noise to ocean
        if(earthSpecular.r > 0.0) {
            vec2 simplexVec = v_Texcoord * 50.0;
            float s_contrib = simplexNoise(simplexVec);
            float t_contrib = simplexNoise(vec2(s_contrib, 50.0 * u_time));
            float noiseCenter = 0.5 * s_contrib * t_contrib;

            vec2 v_TexcoordRight = v_Texcoord * 50.0 + vec2(1.0 / 1024.0, 0.0);
            s_contrib = simplexNoise(v_TexcoordRight);
            t_contrib = simplexNoise(vec2(s_contrib, 50.0 * u_time));
            float noiseRight = 0.5 * s_contrib * t_contrib;

            vec2 v_TexcoordAbove = v_Texcoord * 50.0 + vec2(0.0, 1.0 / 512.0);
            s_contrib = simplexNoise(v_TexcoordAbove);
            t_contrib = simplexNoise(vec2(s_contrib, 50.0 * u_time));
            float noiseAbove = 0.5 * s_contrib * t_contrib;           
            
            // perturbedNormal.x = .0125 * simplexNoise(45.0*vec2(v_positionMC)-22.0*vec2(u_time*0.1, u_time*0.1)) *simplexNoise(20.0*vec2(v_positionMC.z-15.0*u_time*0.1,v_positionMC.y-7.0*u_time*0.1));
            perturbedNormal = normalize(vec3(noiseCenter - noiseRight, noiseCenter - noiseAbove, 0.2));
            normal = normalize(eastNorthUpToEyeCoordinates(v_positionMC, v_Normal) * perturbedNormal);
        }
        
        // normalized eye-to-position vector in camera coordinates
        vec3 eyeToPosition = normalize(v_Position);
        
        float diffuse = clamp(dot(u_CameraSpaceDirLight, normal), 0.0, 1.0);

        // Check the fragment is on ocean or land
        vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, normal);
        float specular = max(dot(toReflectedLight, -eyeToPosition), 0.0);
        specular = earthSpecular.r * pow(specular, 20.0);

        // Day and night color
        float gammaCorrect = 1.0 / 1.2; //gamma correct by 1/1.2
        vec3 dayColor = ((0.6 * diffuse) + (0.8 * specular)) *  texture2D(u_DayDiffuse, v_Texcoord).rgb;
        vec3 nightColor = texture2D(u_Night, v_Texcoord).rgb;
        // Apply gamma correction to nighttime texture
        nightColor = pow(nightColor,vec3(gammaCorrect));
        
        // Adding clouds
        vec2 v_TexcoordClouds = v_Texcoord + vec2(-u_time, 0.0);
        vec3 cloudsColor = texture2D(u_Cloud, v_TexcoordClouds).rgb;
        vec3 cloudsTrans = texture2D(u_CloudTrans, v_TexcoordClouds).rgb;
        vec3 dayClouds =  mix(max(dot(u_CameraSpaceDirLight, normalize(v_Normal)), 0.0) * cloudsColor, dayColor, cloudsTrans);
        vec3 nightClouds = mix(vec3(0.0), nightColor, cloudsTrans);

        // Clouds with day and night blending
        vec3 color = mix(dayClouds, nightClouds, 1.0 - diffuse);
        
        // Post-processing of rim effect
        float rim = dot(v_Normal, v_Position) + 1.0;
        color += rim > 0.0 ? vec3(rim / 4.0, rim / 2.0, rim / 2.0) : vec3(0.0);

        gl_FragColor = vec4(color, 1.0); 
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }

    vec3 permute(vec3 x) {
      x = ((x*34.0)+1.0)*x;
      return x - floor(x * (1.0 / 289.0)) * 289.0;
    }

    float simplexNoise(vec2 v)
      {
      const vec4 C = vec4(0.211324865405187, 0.366025403784439,  -0.577350269189626, 0.024390243902439); 

      vec2 i  = floor(v + dot(v, C.yy) );
      vec2 x0 = v -   i + dot(i, C.xx);

      vec2 i1;
      i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);

      vec4 x12 = x0.xyxy + C.xxzz;
      x12.xy -= i1;

      i = i - floor(i * (1.0 / 289.0)) * 289.0;

      vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
            + i.x + vec3(0.0, i1.x, 1.0 ));

      vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
      m = m*m ;
      m = m*m ;

      vec3 x = 2.0 * fract(p * C.www) - 1.0;
      vec3 h = abs(x) - 0.5;
      vec3 ox = floor(x + 0.5);
      vec3 a0 = x - ox;

      m *= inversesqrt( a0*a0 + h*h );

      vec3 g;
      g.x  = a0.x  * x0.x  + h.x  * x0.y;
      g.yz = a0.yz * x12.xz + h.yz * x12.yw;
      return 130.0 * dot(m, g);
    }    
</script>

<script id="moon_vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_ModelMoon;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 PositionMoon;
    attribute vec3 NormalMoon;
    attribute vec2 TexcoordMoon;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_PositionMoon;
    varying vec3 v_positionMCMoon;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(NormalMoon,0.0)).xyz;
        v_Texcoord = TexcoordMoon;
        vec4 world = u_ModelMoon * vec4(PositionMoon, 1.0);
        vec4 camera = u_View * world;
        v_PositionMoon = camera.xyz;
        v_positionMCMoon = PositionMoon;

        gl_Position = u_Persp * camera;
    }

</script>

<script id="moon_fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Moon map
    uniform sampler2D u_Moon;

    uniform sampler2D u_BumpMoon;

    uniform mat4 u_InvTrans;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    
    varying vec3 v_PositionMoon;

    varying vec3 v_positionMCMoon;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);

    void main(void)
    {   
        
        // Bump mapping
        vec2 v_TexcoordRight = v_Texcoord + vec2(1.0 / 1024.0, 0.0);
        vec2 v_TexcoordAbove = v_Texcoord + vec2(0.0, 1.0 / 512.0);
        float center = texture2D(u_BumpMoon, v_Texcoord).s;
        float right = texture2D(u_BumpMoon, v_TexcoordRight).s;
        float top = texture2D(u_BumpMoon, v_TexcoordAbove).s;
        vec3 perturbedNormal = normalize(vec3(center - right, center - top, 0.2));
        vec3 normal = normalize(eastNorthUpToEyeCoordinates(v_positionMCMoon, v_Normal) * perturbedNormal);  
       
        // vec3 normal = normalize(v_Normal);
        float diffuse = clamp(dot(u_CameraSpaceDirLight, normal), 0.0, 1.0);

        // Clouds with day and night blending
        vec4 color = texture2D(u_Moon, v_Texcoord); 
        gl_FragColor = mix(color, vec4(0.0), 1.0 - diffuse);      
//        gl_FragColor = vec4(color, 1.0); 
    }
    
    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }
    
</script>

<script id="vs_bg" type="x-shader/x-vertex">
    precision highp float;
    
    attribute vec3 PositionBG;
    attribute vec2 TexcoordBG;

    varying vec2 v_Texcoord;

    void main() {
        v_Texcoord = TexcoordBG;
        gl_Position = vec4(PositionBG, 1.0);
    }
</script>

<script id="fs_bg" type="x-shader/x-fragment">
    precision highp float;

    uniform sampler2D u_bg;
    
    varying vec2 v_Texcoord;

    void main() {
            gl_FragColor = vec4(texture2D(u_bg, v_Texcoord).rgb, 1.0);  // green
    }
    </script>
<script src ="gl-matrix.js" type ="text/javascript"></script>
<script src ="webGLUtility.js" type ="text/javascript"></script>
<script src ="stats.js/build/stats.min.js" type ="text/javascript"></script>
<script src ="frag_globe.js" type ="text/javascript"></script>
</body>

</html>
