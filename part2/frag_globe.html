<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
</head>

<body>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1024" height="768" tabindex="1"></canvas>

<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        v_positionMC = Position;
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;

    uniform float u_time;
    uniform mat4 u_InvTrans;

    uniform int u_RenderMode;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);
    vec3 calcBump(vec3 normal);
    void renderBase();              //for rendering base globe

    void main(){
        if (u_RenderMode == 48){
            renderBase();
        }
        else{
            gl_FragColor = vec4(1.0);
        }
    
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }

    vec3 calcBump(vec3 normal){
        
        //read 3 texels for bump values
        float center = texture2D(u_Bump, v_Texcoord).r;
        float top = texture2D(u_Bump, vec2(v_Texcoord.s, v_Texcoord.t+(1.0/512.0))).r;
        float right = texture2D(u_Bump, vec2(v_Texcoord.s+(1.0/1024.0), v_Texcoord.t)).r;
        vec3 bumpNormal = normalize(vec3(center - right, center - top, 0.2));

        //transform to eye space and return
        return eastNorthUpToEyeCoordinates(normalize(v_positionMC), normal) * bumpNormal;
    }

    void renderBase(void)
    {
        // surface normal - normalized after rasterization
        vec3 normal = normalize(v_Normal);
        // normalized eye-to-position vector in camera coordinates
        vec3 eyeToPosition = normalize(v_Position);

        //bump mapping to find new normal
        vec3 bumpNormal = calcBump(normal);

        float signedDiffuse = dot(u_CameraSpaceDirLight, bumpNormal);
        float diffuse = clamp(signedDiffuse, 0.0, 1.0);

        vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, normal);
        
        //only apply specular to ocean, use red channel for transparency
        float specContrib = texture2D(u_EarthSpec, v_Texcoord).r;
        float specular = max(dot(toReflectedLight, -eyeToPosition), 0.0);
        specular = pow(specular, 20.0) * specContrib;

        float gammaCorrect = 1.0/1.3; //gamma correct by 1/1.2

        vec3 dayColor = texture2D(u_DayDiffuse, v_Texcoord).rgb;
        vec3 nightColor = texture2D(u_Night, v_Texcoord).rgb;
        //apply gamma correction to nighttime texture
        nightColor = pow(nightColor,vec3(gammaCorrect));
        
        //color the globe
        vec3 color = ((0.6 * diffuse) + (0.4 * specular)) * dayColor;

        //color the cloud
        vec2 cloudAnim = v_Texcoord;
        cloudAnim.s += u_time; 
        float cloudContrib = texture2D(u_CloudTrans, cloudAnim).r;
        vec3 cloudColor = texture2D(u_Cloud, cloudAnim).rgb;
        cloudColor = ((0.6 * diffuse) + (0.4 * specular)) * cloudColor;
        color = mix(cloudColor, color, cloudContrib);
        
        //if fragment not facing sun, smooth transition to night color
        if (diffuse == 0.0){
            //blend to solid black if covered by cloud
            nightColor = mix(vec3(0.0, 0.0, 0.0), nightColor, cloudContrib);
            color = 2.0*mix(color, nightColor, abs(signedDiffuse));
        }

        //rim lighting
        float rim = max(0.0, dot(v_Normal, v_Position)+1.0);
        color += vec3(rim/4.0, rim/2.0, rim/2.0); 

        gl_FragColor = vec4(color, 1.0);
    }
</script>

<script src ="gl-matrix.js" type ="text/javascript"></script>
<script src ="webGLUtility.js" type ="text/javascript"></script>
<script src ="frag_globe.js" type ="text/javascript"></script>
</body>

</html>
