<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
</head>

<body>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1024" height="768" tabindex="1"></canvas>

<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        v_positionMC = Position;
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;

    //height map
    uniform sampler2D u_Height;

    uniform float u_time;
    uniform mat4 u_InvTrans;

    uniform int u_RenderMode;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);
    vec3 calcBump(vec3 normal);
    void renderBase();              //for rendering base globe
    void renderHeightMap();
    vec3 renderWater();

    vec3 permute(vec3 x) {
        x = ((x*34.0)+1.0)*x;
        return x - floor(x * (1.0 / 289.0)) * 289.0;
    }

    void main(){
        if (u_RenderMode == 49){
            renderHeightMap();
        }
        else{
            renderBase();
        }
    
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }

    vec3 calcBump(vec3 normal){
        
        //read 3 texels for bump values
        float center = texture2D(u_Bump, v_Texcoord).r; 
        float top = texture2D(u_Bump, vec2(v_Texcoord.s, v_Texcoord.t+(1.0/512.0))).r;
        float right = texture2D(u_Bump, vec2(v_Texcoord.s+(1.0/1024.0), v_Texcoord.t)).r; 

        float specContrib = texture2D(u_EarthSpec, v_Texcoord).r;
        float noise = cos(10.0*u_time);

        center += 0.1*sin(v_Texcoord.s * fract(0.6 * u_time )* 200.0)*cos(v_Texcoord.t * fract(0.6 * u_time))* specContrib;
        top += 0.1*cos(v_Texcoord.t+(1.0/512.0) * fract(0.6 * u_time) * 200.0 )*sin(v_Texcoord.s * fract(0.6 * u_time)) * specContrib;
        //right += 0.5*cos(v_Texcoord.s+(1.0/1024.0) * sin(u_time) * 100.0 ) * specContrib;
     
        //center += sin(20.0*height)* noise * specContrib;
        //top += sin(30.0 * height) * noise* specContrib;
        //right += sin(30.0 * height) * cos(noise)* specContrib;
        //top += cos(height) * specContrib;
        //right += sin(height) * specContrib;
        //center += 0.01 * sin(1000.0*u_time + 500.0)* cos(50.0*u_time) * specContrib;
        //top += 0.01*cos(1000.0*u_time + 900.0)* cos(50.0*u_time) * specContrib;
        //right += 10.0*sin(100.0*u_time + v_Position.y + v_Position.x + 300.0) * cos(50.0*u_time) * specContrib;
    
    /*    float waveIntensity = 0.5;
        center = ((1.2 - waveIntensity) * sin(33.0 * v_Texcoord.s*v_positionMC.y + 53.0 * (1.2 - waveIntensity) * (abs(fract(0.75*u_time) - 0.5) + 0.2))) 
                                           * ((1.2 - waveIntensity) * cos(50.0 * v_Texcoord.t*v_positionMC.y + 35.0 * (1.2 - waveIntensity) * (abs(fract(0.75*u_time) - 0.5) + 0.2)));
        right =  ((1.2 - waveIntensity) * sin(33.0 *(v_Texcoord.s+(1.0/1024.0))  + 53.0 * (1.2 - waveIntensity) * (abs(fract(0.75*u_time) - 0.5) + 0.2))) 
                                           * ((1.2 - waveIntensity) * cos(50.0 * v_Texcoord.t                                    + 35.0 * (1.2 - waveIntensity) * (abs(fract(0.75*u_time) - 0.5) + 0.2)));
        top =          ((1.2 - waveIntensity) * sin(33.0 * v_Texcoord.s                              + 53.0 * (1.2 - waveIntensity) * (abs(fract(0.75*u_time) - 0.5) + 0.2))) 
                                           * ((1.2 - waveIntensity) * cos(50.0 *(v_Texcoord.t+(1.0/512.0))   + 35.0 * (1.2 - waveIntensity) * (abs(fract(0.75*u_time) - 0.5) + 0.2)));
    */
        vec3 bumpNormal = normalize(vec3(center - right, center - top, 0.2));
        
        //transform to eye space and return
        return eastNorthUpToEyeCoordinates(normalize(v_positionMC), normal) * bumpNormal;
    }

    void renderBase(void)
    {
        // surface normal - normalized after rasterization
        vec3 normal = normalize(v_Normal);
        // normalized eye-to-position vector in camera coordinates
        vec3 eyeToPosition = normalize(v_Position);

        //bump mapping to find new normal
        vec3 bumpNormal = calcBump(normal);
        
        float signedDiffuse = dot(u_CameraSpaceDirLight, bumpNormal);
        float diffuse = clamp(signedDiffuse, 0.0, 1.0);

        vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, normal);
        
        //only apply specular to ocean, use red channel for transparency
        float specContrib = texture2D(u_EarthSpec, v_Texcoord).r;
        float specular = max(dot(toReflectedLight, -eyeToPosition), 0.0);
        specular = pow(specular, 20.0) * specContrib;
        
        float gammaCorrect = 1.0/1.3; //gamma correct by 1/1.2

        vec3 dayColor = texture2D(u_DayDiffuse, v_Texcoord).rgb;
        vec3 nightColor = texture2D(u_Night, v_Texcoord).rgb;
        //apply gamma correction to nighttime texture
        nightColor = pow(nightColor,vec3(gammaCorrect));
        
        //color the globe
        vec3 color = ((0.6 * diffuse) + (0.4 * specular)) * dayColor;
        
        //color the cloud
        vec2 cloudAnim = v_Texcoord;
        //cloudAnim.s += u_time; 
        float cloudContrib = texture2D(u_CloudTrans, cloudAnim).r;
        vec3 cloudColor = texture2D(u_Cloud, cloudAnim).rgb;
        cloudColor = ((0.6 * diffuse) + (0.4 * specular)) * cloudColor;
        color = mix(cloudColor, color, cloudContrib);
        
        //if fragment not facing sun, smooth transition to night color
        if (diffuse == 0.0){
            //blend to solid black if covered by cloud
            nightColor = mix(vec3(0.0, 0.0, 0.0), nightColor, cloudContrib);
            color = 2.0*mix(color, nightColor, abs(signedDiffuse));
        }

        //rim lighting
        float rim = max(0.0, dot(v_Normal, v_Position)+1.0);
        color += vec3(rim/4.0, rim/2.0, rim/2.0); 

        gl_FragColor = vec4(color, 1.0);
    }

    vec3 renderWater(){

       float isWater = texture2D(u_EarthSpec, v_Texcoord).r;
       vec3 newPos = v_Position;
       newPos.y = 0.0 ;       
       return newPos;
    }

    void renderHeightMap(){
        vec3 normal = calcBump(normalize(v_Normal));
        float diffuse = dot(u_CameraSpaceDirLight, normal);

        float height = texture2D(u_Height, v_Texcoord).r;
       
        //interpolate color based on height 
        vec3 color;
        
        if(height < 0.5)
            color = mix(vec3(0, 0, 1), vec3(0, 1, 0), height);
        else
            color = mix(vec3(0, 1, 0), vec3(1, 0, 0), height);

        color = (0.4*diffuse + 1.0)*color;
        gl_FragColor = vec4(color, 1.0);
    }

</script>

<script src ="gl-matrix.js" type ="text/javascript"></script>
<script src ="webGLUtility.js" type ="text/javascript"></script>
<script src ="frag_globe.js" type ="text/javascript"></script>
</body>

</html>
