<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
</head>

<body>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1024" height="768" tabindex="1"></canvas>

<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        v_positionMC = Position;
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;
	//Wave Intensity
	uniform sampler2D u_Wave;

    uniform float u_time;
    uniform mat4 u_InvTrans;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);

    void main(void)
    {
		// what is water and what is land?
		float earthSpecMask = texture2D(u_EarthSpec, v_Texcoord).r;
		float waveIntensity = texture2D(u_Wave, v_Texcoord).r;
		
		//read 3 texels from bump map for land, noise for water
		float bumpCenter, bumpRight, bumpTop;
		if (earthSpecMask == 0.00) {
			bumpCenter = texture2D(u_Bump, vec2(v_Texcoord.s, v_Texcoord.t)).r;
			bumpRight  = texture2D(u_Bump, vec2(v_Texcoord.s + (1.0/512.0), v_Texcoord.t)).r;
			bumpTop    = texture2D(u_Bump, vec2(v_Texcoord.s, v_Texcoord.t + (1.0/1024.0))).r;
		} else {
			bumpCenter = ((1.2 - waveIntensity) * sin(33.0 * v_Texcoord.s*v_positionMC.y + 53.0 * (1.2 - waveIntensity) * (abs(fract(0.75*u_time) - 0.5) + 0.2))) 
					   * ((1.2 - waveIntensity) * cos(50.0 * v_Texcoord.t*v_positionMC.y + 35.0 * (1.2 - waveIntensity) * (abs(fract(0.75*u_time) - 0.5) + 0.2)));
			bumpRight =  ((1.2 - waveIntensity) * sin(33.0 *(v_Texcoord.s+(1.0/1024.0))  + 53.0 * (1.2 - waveIntensity) * (abs(fract(0.75*u_time) - 0.5) + 0.2))) 
					   * ((1.2 - waveIntensity) * cos(50.0 * v_Texcoord.t			   	 + 35.0 * (1.2 - waveIntensity) * (abs(fract(0.75*u_time) - 0.5) + 0.2)));
			bumpTop = 	 ((1.2 - waveIntensity) * sin(33.0 * v_Texcoord.s 			     + 53.0 * (1.2 - waveIntensity) * (abs(fract(0.75*u_time) - 0.5) + 0.2))) 
					   * ((1.2 - waveIntensity) * cos(50.0 *(v_Texcoord.t+(1.0/512.0))   + 35.0 * (1.2 - waveIntensity) * (abs(fract(0.75*u_time) - 0.5) + 0.2)));
		}
		vec3 tangentSpaceBumpNormal = normalize(vec3(bumpCenter-bumpRight, bumpCenter-bumpTop, 0.2));
		
		
        // surface normal - normalized after rasterization
        vec3 v_normalEC = normalize(v_Normal);
        // normalized eye-to-position vector in camera coordinates
        vec3 eyeToPosition = normalize(v_Position);
        
		mat3 tangentToEyeSpace = eastNorthUpToEyeCoordinates(v_positionMC, v_normalEC);
		vec3 normal = normalize(tangentToEyeSpace*tangentSpaceBumpNormal);
		
        float diffuse = clamp(dot(u_CameraSpaceDirLight, normal), 0.0, 1.0);

        vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, normal);
        float specular = max(dot(toReflectedLight, -eyeToPosition), 0.0);
        specular = pow(specular, 20.0);

        float gammaCorrect = 1.0/1.1; //gamma correct by 1/1.1

        vec3 dayColor = texture2D(u_DayDiffuse, v_Texcoord).rgb;
        vec3 nightColor = texture2D(u_Night, v_Texcoord).rgb;
		
        //apply gamma correction to nighttime texture
        nightColor = pow(nightColor,vec3(gammaCorrect));
		
		//apply specular mask to daytime texture
		dayColor = ((0.6 * diffuse) + (0.4 * earthSpecMask * specular)) * dayColor;
		
		//apply clouds
		vec2 cloudTexCoord = vec2(v_Texcoord.s - u_time/5.0, v_Texcoord.t);
		vec3 cloudColor = texture2D(u_Cloud, cloudTexCoord).rgb;
		float cloudTrans = texture2D(u_CloudTrans, cloudTexCoord).r;
		dayColor = mix(cloudColor, dayColor, cloudTrans);
		nightColor = mix(vec3(0.0), nightColor, cloudTrans);

		float reDiffuse = clamp(diffuse*2.5, 0.0, 1.0);
        vec3 color = mix(nightColor, dayColor, reDiffuse);
        gl_FragColor = vec4(color, 1.0);
		
		//rim lighting
		float rimFactor = dot(v_Normal, v_Position) + 1.0;
		if (rimFactor > 0.0) {
			vec4 rimColor = vec4(rimFactor/4.0, rimFactor/2.0, rimFactor/2.0, 1.0);
			gl_FragColor += rimColor;
		}
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }
</script>

<script src ="gl-matrix.js" type ="text/javascript"></script>
<script src ="webGLUtility.js" type ="text/javascript"></script>
<script src ="frag_globe.js" type ="text/javascript"></script>
</body>

</html>
