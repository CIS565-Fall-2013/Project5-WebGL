<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
</head>

<body>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1024" height="768" tabindex="1"></canvas>

<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        v_positionMC = Position;
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;

    uniform float u_time;
    uniform mat4 u_InvTrans;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);

    //Perlin noise stuff
    float noise(float x, float y) {
      return fract(sin(dot(vec2(x,y) ,vec2(12.9898,78.233))) * 43758.5453);
    }
    
    float smoothNoise(float x, float y) {
      float corners = (noise(x-1.0, y-1.0) + noise(x+1.0, y-1.0) + noise(x-1.0, y+1.0) + noise(x+1.0, y+1.0)) / 16.0;
      float sides = (noise(x-1.0, y) + noise(x+1.0, y) + noise(x, y-1.0) + noise(x, y+1.0)) / 8.0;
      float center = noise(x, y) / 4.0;
      return corners + sides + center;
    }

    float interpolate(float a, float b, float t) {
      return a * (1.0-t) + b * t;
    }

    float interpolateNoise(float x, float y) {
      float int_x = floor(x);
      float fract_x = x - int_x;
      float int_y = floor(y);
      float fract_y = y - int_y;

      float v1 = smoothNoise(int_x, int_y);
      float v2 = smoothNoise(int_x+1.0, int_y);
      float v3 = smoothNoise(int_x, int_y+1.0);
      float v4 = smoothNoise(int_x+1.0, int_y+1.0);

      float i1 = interpolate(v1, v2, fract_x);
      float i2 = interpolate(v3, v4, fract_x);

      return interpolate(i1, i2, fract_y);
    }

    float perlin(vec2 v) {
      float total = 0.0;
      float p = 1.1;

      for (int i=0; i<6; i++) {
        float f = pow(2.0, float(i));
        float amp = pow(p, float(i));
        total =  total + interpolateNoise(v.x * f, v.y * f) * amp;
      }

      return 3.0*total;
    }


    void main(void)
    {
        float center = texture2D(u_Bump, v_Texcoord).r;
        vec2 rightTexcoord = v_Texcoord + vec2(1.0/1024.0, 0.0);
        float right = texture2D(u_Bump, rightTexcoord).r;
        vec2 topTexcoord = v_Texcoord + vec2(0.0, 1.0/512.0);
        float top = texture2D(u_Bump, topTexcoord).r;
        vec3 landNormal = normalize(vec3(center - right, center - top, 0.2));
        
        vec3 normalEC = normalize(v_Normal);
        mat3 transform = eastNorthUpToEyeCoordinates(v_positionMC, normalEC);

        landNormal = normalize(transform * landNormal);

        vec2 seaPosition = vec2(v_Position.x-3.0*u_time, v_Position.y);
        float dx = 0.1;
        float seaCenter = perlin(seaPosition);
        float seaRight = perlin(seaPosition + vec2(dx, 0.0));
        float seaTop = perlin(seaPosition + vec2(0.0, dx));

        // float r = sqrt(v_Position.x * v_Position.x + v_Position.y * v_Position.y + v_Position.z * v_Position.z);
        // float theta = asin(v_Position.y/r);
        // float phi = atan(v_Position.z/v_Position.x);
        // float seaCenter = perlin(vec2(phi, theta));

        // float rightPhi = phi + 0.01;
        // float seaRight = perlin(vec2(rightPhi, theta));

        // float topTheta = theta + 0.01;
        // float seaTop = perlin(vec2(phi, topTheta));        

        vec3 seaNormal = normalize(vec3(seaCenter - seaRight, seaCenter - seaTop, 1.0));
        //seaNormal = normalize(transform * seaNormal);

        float isOcean = texture2D(u_EarthSpec, v_Texcoord).r;
        vec3 normal = isOcean == 1.0 ? seaNormal : landNormal;

        // normalized eye-to-position vector in camera coordinates
        vec3 eyeToPosition = normalize(v_Position);
        
        float diffuse = clamp(dot(u_CameraSpaceDirLight, normal), 0.0, 1.0);

        vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, normal);
        float specular = max(dot(toReflectedLight, -eyeToPosition), 0.0);
        specular = pow(specular, 20.0);

        float gammaCorrect = 0.9; //gamma correct by 1/1.2

        vec3 dayColor = texture2D(u_DayDiffuse, v_Texcoord).rgb;
        vec3 nightColor = texture2D(u_Night, v_Texcoord).rgb;
        //apply gamma correction to nighttime texture
        nightColor = pow(nightColor,vec3(gammaCorrect));

        float kd = isOcean == 1.0 ? 0.6 : 1.0;
        float ks = 1.0 - kd;
        dayColor = ((kd * diffuse) + (ks * specular)) * dayColor;

        // vec2 cloudTexcoord = v_Texcoord + vec2(u_time, 0.0);
        // float cloudTrans = texture2D(u_CloudTrans, cloudTexcoord).r;
        // vec3 cloudColor = texture2D(u_Cloud, cloudTexcoord).rgb;
        // dayColor = mix(cloudColor, dayColor, cloudTrans);
        // nightColor = mix(vec3(0.0), nightColor, cloudTrans);

        vec3 color = mix(nightColor, dayColor, diffuse);

        //rim
        float rim = dot(v_Normal, v_Position) + 1.0;
        if (rim > 0.0) {
            color += vec3(rim/4.0, rim/2.0, rim/2.0);
        }

        gl_FragColor = vec4(color, 1.0);
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }
</script>

<script src ="gl-matrix.js" type ="text/javascript"></script>
<script src ="webGLUtility.js" type ="text/javascript"></script>
<script src ="stats.min.js" type="text/javascript"></script>
<script src ="frag_globe.js" type ="text/javascript"></script>
</body>

</html>
