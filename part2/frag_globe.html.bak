<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
</head>

<body>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1024" height="768" tabindex="1"></canvas>

<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        v_positionMC = Position;
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;

    uniform float u_time;
    uniform mat4 u_InvTrans;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);

    void main(void)
    {
        // surface normal - normalized after rasterization
        vec2 upTexcoord=v_Texcoord;
        vec2 rightTexcoord=v_Texcoord;
        upTexcoord.t-=1.0/512.0;
        rightTexcoord.s+=1.0/1024.0;
        
        float center=texture2D(u_Bump,v_Texcoord).r;
        float theright=texture2D(u_Bump, rightTexcoord).r;
        float theUp=texture2D(u_Bump, upTexcoord).r;
        
        vec3 turbnormal=normalize(vec3(center-theright, center-theUp,0.2));
        vec3 normal=normalize(eastNorthUpToEyeCoordinates(v_positionMC, v_Normal)*turbnormal);
        
        float rimfactor=dot(v_Normal, v_Position)+1.0;
        
        //vec3 normal = normalize(v_Normal);
        // normalized eye-to-position vector in camera coordinates
        vec3 eyeToPosition = normalize(v_Position);
        
        float dotlightnorm=dot(u_CameraSpaceDirLight, normal);
        float diffuse = clamp(dotlightnorm, 0.0, 1.0);

        vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, normal);
        float specular = max(dot(toReflectedLight, -eyeToPosition), 0.0);
        specular = pow(specular, 50.0);

        float gammaCorrect = 1.0/1.2; //gamma correct by 1/1.2

        vec3 dayColor = texture2D(u_DayDiffuse, v_Texcoord).rgb;
        vec3 nightColor = texture2D(u_Night, v_Texcoord).rgb;
        vec3 oceanStatus = texture2D(u_EarthSpec, v_Texcoord).rgb;
        
        vec2 v_Texcoord2=v_Texcoord;
        v_Texcoord2.s-=0.0005*u_time;
        vec3 cloudColor = texture2D(u_Cloud, v_Texcoord2).rgb;
        vec3 cloudTrans = texture2D(u_CloudTrans, v_Texcoord2).rgb;
        
        
        bool isOcean=length(oceanStatus)>0.5;
        //apply gamma correction to nighttime texture
        nightColor = pow(nightColor,vec3(gammaCorrect));

				vec3 color;
				if(dotlightnorm>0.0)
				{
						cloudColor*=0.6*diffuse+0.8*specular;
        		color = ((0.6 * diffuse) + (0.8 * specular*(isOcean?1.0:0.0))) * dayColor;
        		color = mix(cloudColor, color, cloudTrans.r);
        }
        else
        {
        	color = (-dotlightnorm)*nightColor;
        	cloudColor*=(1.0-cloudTrans.g)*0.2;
        	color+=cloudColor;
        	//color = mix(color, cloudColor,clamp(length(color),0.0,1.0));
        }
        
        if(rimfactor>0.0f)
        {
        	color+=(rimfactor/4.0, rimfactor/2.0, rimfactor/2.0, 1.0);
        }
        gl_FragColor = vec4(color, 1.0);
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }
</script>

<script src ="gl-matrix.js" type ="text/javascript"></script>
<script src ="webGLUtility.js" type ="text/javascript"></script>
<script src ="frag_globe.js" type ="text/javascript"></script>
</body>

</html>
